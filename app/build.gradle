plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {

    defaultConfig {
        applicationId "com.timmy.codelab.cicd.circleci"
        versionCode 1
        versionName "1.0"
    }

    def signingPropertiesFile = file("signing.properties")
    def signingProperties = null
    if (signingPropertiesFile.exists()) {
        signingProperties = new Properties()
        signingProperties.load(new FileInputStream(signingPropertiesFile))
    } else {
        println("Local signing.properties file not found, falling back to default.")
    }

    if (signingProperties != null) {
        signingConfigs {
            release {
                storeFile file(signingProperties['KEYSTORE_FILE'])
                storePassword signingProperties['KEYSTORE_PASSWORD']
                keyAlias signingProperties['KEY_ALIAS']
                keyPassword signingProperties['KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            def signingConfigLocal = signingConfigs.findByName("release")
            if (signingConfigLocal != null) {
                signingConfig signingConfigLocal
            }
        }
    }
}

dependencies {
    // AndroidX
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.9.0'

    // Google
    implementation 'com.google.android.material:material:1.7.0'
}