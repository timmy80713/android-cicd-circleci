plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {

    compileSdk 33

    defaultConfig {
        applicationId "com.timmy.codelab.cicd.circleci"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def signingPropertiesFile = file("signing.properties")
    def signingProperties = null
    if (signingPropertiesFile.exists()) {
        signingProperties = new Properties()
        signingProperties.load(new FileInputStream(signingPropertiesFile))
    } else {
        println("Local signing.properties file not found, falling back to default.")
    }

    if (signingProperties != null) {
        signingConfigs {
            release {
                storeFile file(signingProperties['KEYSTORE_FILE'])
                storePassword signingProperties['KEYSTORE_PASSWORD']
                keyAlias signingProperties['KEY_ALIAS']
                keyPassword signingProperties['KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            def signingConfigLocal = signingConfigs.findByName("release")
            if (signingConfigLocal != null) {
                signingConfig signingConfigLocal
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.9.0'

    // Google
    implementation 'com.google.android.material:material:1.7.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}